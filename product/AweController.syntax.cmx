/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package $dynamicPackageName;
use cm.syntax;

public const symbol UPDATE_PRODUCT_SYMBOL = "Awe$$UpdateProduct$$";
public definition @visibility AweController @controllerName=id 
    '<' @productType=type ',' @propType=type '>' @ext=["extends" type]?   
    '{' @extraMembers=[classMember]* '}'  { 
    
    SMembers members(propType.src);
    members << member { 

        @visibility @propType aweProps(this);

        @visibility @productType product(aweProps.model);
        
        public aweQuickPropertiesBinder(aweProps); 

        public Primitive3D get3D(FetchEnv3D env) {
            aweProps.initProps();
            return product.get3D(env);
        }

        public void drawGraphs(LayerBuffer lb, View view, GeometricFinder finder=null) {
            aweProps.initProps();
            Graph g = product.get2D();
            if(g)
                g.draw(lb);
        }

        public void invalidate(symbol k) {
            if (k == UPDATE_PRODUCT_SYMBOL) { 
	            this.alignAllSnapped(tryRealign=false);
                product.refresh();
                invalidate();
                space.invalidateSelection();
                return;
            }

            super(..);
        } 

        /// Todo: improve on this
        public box localBound() {
            return product.get3D(FetchEnv3D(detailLevel3D.lousy)).bound;
        }

    };

    var baseClassName = controllerName.name # "___Base";
    if (ext) { 
        definition { 
            @visibility class @baseClassName extends @ext { 
                @members;
            }
        };
    } else { 
        definition { 
            @visibility class @baseClassName extends Snapper { 
                @members;
            }
        };
    }

    SMembers finalMembers(propType.src);
    if (extraMembers) { 
        for (m in extraMembers) { 
            finalMembers << m;
        }
    }
    definition { 
        @visibility class @controllerName extends @baseClassName { 
            @finalMembers;
        }
    };
}